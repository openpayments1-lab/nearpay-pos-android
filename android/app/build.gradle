apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    namespace "app.cashmgmtnp.pos"
    compileSdk rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "app.cashmgmtnp.pos"
        minSdkVersion 30  // Required for Tap-to-Pay on Phone (SoftPOS)
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        
        // NearPay Google Cloud Project Number for Tap-to-Pay
        resValue "string", "google_cloud_project_number", System.getenv("GOOGLE_CLOUD_PROJECT_NUMBER") ?: "392076631783"
        aaptOptions {
             // Files and dirs to omit from the packaged assets dir, modified to accommodate modern web apps.
             // Default: https://android.googlesource.com/platform/frameworks/base/+/282e181b58cf72b6ca770dc7ca5f91f135444502/tools/aapt/AaptAssets.cpp#61
            ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
        }
    }
    
    signingConfigs {
        release {
            storeFile file("../../certs/nearpay-sandbox.keystore")
            storePassword "nearpaysandbox2025"
            keyAlias "nearpay-sandbox-key"
            keyPassword "nearpaysandbox2025"
        }
        debug {
            storeFile file("../../certs/nearpay-sandbox.keystore")
            storePassword "nearpaysandbox2025"
            keyAlias "nearpay-sandbox-key"
            keyPassword "nearpaysandbox2025"
        }
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    
    kotlinOptions {
        jvmTarget = '17'
    }
    
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }
}

repositories {
    flatDir{
        dirs '../capacitor-cordova-android-plugins/src/main/libs', 'libs'
    }
    maven { url 'https://jitpack.io' }
    maven { url 'https://developer.huawei.com/repo/' }
    maven {
        url 'https://gitlab.com/api/v4/projects/37026421/packages/maven'
        credentials(HttpHeaderCredentials) {
            name = 'Private-Token'
            value = System.getProperty('nearPayGitLabToken') ?: System.getenv('NEARPAY_GITLAB_TOKEN') ?: ''
        }
        authentication {
            header(HttpHeaderAuthentication)
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$androidxCoordinatorLayoutVersion"
    implementation "androidx.core:core-splashscreen:$coreSplashScreenVersion"
    implementation project(':capacitor-android')
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
    implementation project(':capacitor-cordova-android-plugins')
    
    // Kotlin standard library
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    
    // NearPay Terminal SDK v2.1.95 - SoftPOS (Tap-to-Pay)
    implementation 'io.nearpay:terminalsdk-release:2.1.95'
    implementation 'com.google.android.gms:play-services-location:21.0.1'
    implementation 'com.huawei.hms:location:6.11.0.301'
}

apply from: 'capacitor.build.gradle'

// Force Java 17 compatibility (override capacitor.build.gradle's Java 21 setting)
// Kotlin 1.9.22 requires Java 17, not Java 21
android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

try {
    def servicesJSON = file('google-services.json')
    if (servicesJSON.text) {
        apply plugin: 'com.google.gms.google-services'
    }
} catch(Exception e) {
    logger.info("google-services.json not found, google-services plugin not applied. Push Notifications won't work")
}
